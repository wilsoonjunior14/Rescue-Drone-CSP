-----------------------------------------------
----------- Drone Specification ---------------
-- Author: Francisco Wilson Rodrigues Junior --
------------- Date: 01/08/2021 ---------------- 
-----------------------------------------------

----------------------
-- Global Variables --
----------------------
datatype ONOFF = on | off
datatype STATUS = available | unavailable
datatype MESSAGE = SWITCHON | SWITCHOFF | TAKEOFF | LAND | ORIGIN | FOUND | MOVING | DELIVER
datatype FINDER_STATES = OFF | LOOKING | DELIVERING | RETURNING | INIT

IDS = {1}
VALUES = {1..2}

WAIT_TIME = IDS
LINEAR_VELOCITY = {2}

channel switch : IDS.ONOFF
channel origin, land, found : IDS
channel takeoff, looking : IDS
channel move : IDS.LINEAR_VELOCITY
channel delivery, returning, turnBack : IDS

channel wait : IDS.WAIT_TIME

channel in, out : MESSAGE.FINDER_STATES

------------------------------------------------------------
-- Mechanism of communication between drone and controller --
------------------------------------------------------------
BUFFER(<>) = in?x -> BUFFER(<x>)
BUFFER(<x>^tr) = 
        in?y -> BUFFER(<x>^tr^<y>)
        []
        out!x -> BUFFER(tr)

---------------------------------
-------- The drone system -------
---------------------------------
DRONE(id) = 
        switch.id.on -> in.SWITCHON.OFF -> DRONE(id)
        []
        out.TAKEOFF.LOOKING -> takeoff.id -> looking.id -> DRONE(id)
        []
        out.TAKEOFF.RETURNING -> takeoff.id -> returning.id -> turnBack.id -> DRONE(id)
        []
        out.LAND.LOOKING -> land.id -> delivery.id -> wait.id?TOP -> in.DELIVER.DELIVERING -> DRONE(id) 
        [] 
        out.LAND.RETURNING -> land.id -> wait.id?TOP -> DRONE(id)
        []
        out.SWITCHOFF.OFF -> switch.id.off -> DRONE(id)
        []
        out.MOVING?actualState -> move.id?lv -> 
            if actualState == RETURNING
            then 
                origin.id -> in.ORIGIN.OFF -> DRONE(id)
            else if actualState == LOOKING
            then
                found.id -> in.FOUND.LOOKING -> DRONE(id)
            else
                DRONE(id)

---------------------------------
-- The controller of the drone --
---------------------------------

FINDER(id) =
        out?action?state -> FINDER_DO_ACTION(id, state, action)
            

FINDER_DO_ACTION(id, state, action) = 
            if action == SWITCHON and state == OFF
            then
                in.TAKEOFF.LOOKING -> in.MOVING.LOOKING -> FINDER(id)
            else if action == FOUND and state == LOOKING
            then
                in.LAND.LOOKING -> FINDER(id)
            else if action == DELIVER and state == DELIVERING
            then
                in.TAKEOFF.RETURNING -> in.LAND.RETURNING -> in.MOVING.RETURNING -> FINDER(id)
            else if action == ORIGIN and state == OFF
            then
                in.SWITCHOFF.OFF -> FINDER(id)
            else
                FINDER(id)

PLATFORM = (FINDER(1) ||| DRONE(1)) [|{|in, out|}|] BUFFER(<>)

-- Asserts for Drone (Hardware) --
assert DRONE(1) :[deadlock free]
assert DRONE(1) :[deterministic]
assert DRONE(1) :[livelock free]

-- Asserts for Finder (Software) --
assert FINDER(1) :[deadlock free]
assert FINDER(1) :[deterministic]
assert FINDER(1) :[livelock free]

-- Assert for Finder' which contains same behavior of original Finder
FINDER'(id) = out.SWITCHON.OFF -> in.TAKEOFF.LOOKING -> in.MOVING.LOOKING -> FINDER'(id)

assert FINDER(1) [T= FINDER'(1)

-- Assert for Finder'' which contains many delays events represented by wait?TOP
FINDER''(id) = out.SWITCHON.OFF -> wait?TOP -> wait?TOP -> in.TAKEOFF.LOOKING -> wait?TOP -> wait?TOP -> in.MOVING.LOOKING -> FINDER''(id)

assert FINDER(1) [T= FINDER''(1)

-- Assert for Finder''' which contains incorrect behavior comparated with original model
FINDER'''(id) = out.SWITCHON.OFF -> in.LAND.LOOKING -> in.TAKEOFF.LOOKING -> in.MOVING.LOOKING -> FINDER'''(id)

-- Assert for Finder'''' which contains deadlock state
assert FINDER(1) [T= FINDER'''(1)
FINDER''''(id) =
        out?action?state -> 
            if action == SWITCHON and state == OFF
            then
                in.TAKEOFF.LOOKING -> in.MOVING.LOOKING -> FINDER(id)
            else 
                STOP

assert FINDER''''(1) :[deadlock free]
